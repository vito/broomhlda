use("atomy")
use("hl/define")

Lexer = lexer:
  name: "Atomy"
  aliases: "atomy"
  extensions: [".ay"]
  mimetypes: "text/x-atomy"
  start: .root
  flags: Regexp::MULTILINE

  @@identifier =
    raw"[\p{Ll}_][\p{L}\d\-_]*"

  @@operator = raw"[\p{S}!@#%&*\-\\:.\/\?]+"

  @@reserved =
    raw"(#language|macro|self)\b(?!" + @@operator + ")"

  @@ascii = [
    "NUL","SOH","[SE]TX","EOT","ENQ","ACK",
    "BEL","BS","HT","LF","VT","FF","CR","S[OI]","DLE",
    "DC[1-4]","NAK","SYN","ETB","CAN",
    "EM","SUB","ESC","[FGRU]S","SP","DEL"
  ]


  -- starting point for lexing
  lex(root):
    -- comments
    r"--.*?$" is(comment.single)
    r"{-" is(comment.multiline) => in-comment

    -- primitive constants
    r"true|false|nil" is(keyword.constant)

    -- constants
    r"[A-Z][a-zA-Z0-9_]*" is(name.constant)

    -- floats
    r"[\+\-]?\d+[eE][\+\-]?\d+" is(number.float)
    r"[\+\-]?\d+\.\d+([eE][\+\-]?\d+)?" is(number.float)

    -- integers
    r"[\+\-]?0[oO][0-7]+" is(number.octal)
    r"[\+\-]?0[xX][\da-fA-F]+" is(number.hexadecimal)
    r"[\+\-]?\d+" is(number.integer)

    -- reserved words
    Regexp new(@@reserved, Regexp::KCODE_UTF8) is(keyword.reserved)

    -- special variables
    Regexp new("@@" + @@identifier, Regexp::KCODE_UTF8) is(name.variable.class)
    Regexp new("\\$" + @@identifier, Regexp::KCODE_UTF8) is(name.variable.global)
    Regexp new("@" + @@identifier, Regexp::KCODE_UTF8) is(name.variable.instance)

    -- particles
    Regexp new("\\." + @@identifier, Regexp::KCODE_UTF8) is(string.symbol)

    -- macro-quotes
    Regexp new("(" + @@identifier + ")(\")", Regexp::KCODE_UTF8) is(by-groups(name, string)) => macro-quote

    -- identifiers
    Regexp new(@@identifier, Regexp::KCODE_UTF8) is(name)

    -- operators
    Regexp new(@@operator, Regexp::KCODE_UTF8) is(operator)

    -- strings
    r"\"" is(string) => in-string

    -- whitespace
    r"\s+" is(text)

    -- quoting expressions
    Regexp new("['`]" + @@identifier, Regexp::KCODE_UTF8) is(string.symbol)
    r"['`]" is(string.symbol)

    -- unquote
    Regexp new("~" + @@identifier, Regexp::KCODE_UTF8) is(string.interpolate)
    r"~" is(string.interpolate)

    -- punctuation
    r"[\[\](),;{}|]" is(punctuation)


  -- inside of a block comment; handle nesting
  lex(in-comment):
    r"[^\-\{\}]+" is(comment.multiline)
    r"{-" is(comment.multiline) -> push
    r"-}" is(comment.multiline) -> pop
    r"[-{}]" is(comment.mutline)


  -- inside of a string; contain escapes
  lex(in-string):
    r"[^\\\"]+" is(string)
    r"\\" is(string.escape) => escape
    r"\"" is(string) -> pop


  -- match delimiters in a macro-quote, skipping escapes
  lex(macro-quote) [d]:
    r"[^\\\"]" is(string)
    r"\\." is(string)
    r"\"" is(string) -> pop


  -- all of our character escapes
  lex(escape):
    r"[nsrtvfbae\\\"]" is(string.escape) -> pop
    r"\^[\]\[A-Z@\^_]" is(string.escape) -> pop
    Regexp new(@@ascii join("|")) is(string.escape) -> pop
    r"o[0-7]+" is(string.escape) -> pop
    r"x[\da-fA-F]+" is(string.escape) -> pop
    r"\d+" is(string.escape) -> pop
    r"\s+\\" is(string.escape) -> pop
